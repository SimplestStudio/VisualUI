cmake_minimum_required(VERSION 3.5)
project(VisualUI)

set(CMAKE_CXX_STANDARD 11)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

include(GNUInstallDirs)

set(INSTALL_SUFFIX "")
if(CMAKE_GENERATOR_PLATFORM STREQUAL "Win32")
    set(INSTALL_SUFFIX "_x86")
elseif(CMAKE_GENERATOR_PLATFORM STREQUAL "x64")
    set(INSTALL_SUFFIX "_x64")
endif()

if(NOT DEFINED INSTALL_SUFFIX OR INSTALL_SUFFIX STREQUAL "")
    if(CMAKE_SIZEOF_VOID_P EQUAL 4)
        set(INSTALL_SUFFIX "_x86")
    elseif(CMAKE_SIZEOF_VOID_P EQUAL 8)
        # By default x64 is without suffix
    endif()
endif()

set(DEFAULT_INSTALL_PREFIX "${CMAKE_CURRENT_SOURCE_DIR}/install${INSTALL_SUFFIX}")

if(CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT)
    set(CMAKE_INSTALL_PREFIX "${DEFAULT_INSTALL_PREFIX}" CACHE PATH "Install prefix" FORCE)
endif()

add_library(VisualUI STATIC)

target_compile_definitions(VisualUI PRIVATE VISUALUI_STATIC)
include("${CMAKE_CURRENT_SOURCE_DIR}/VisualUI.cmake")

set(OUTPUT_DIR "${CMAKE_CURRENT_SOURCE_DIR}/lib/build/$<CONFIG>")
set_target_properties(VisualUI PROPERTIES
    OUTPUT_NAME "visualui"
    ARCHIVE_OUTPUT_DIRECTORY ${OUTPUT_DIR}
    LIBRARY_OUTPUT_DIRECTORY ${OUTPUT_DIR}
    RUNTIME_OUTPUT_DIRECTORY ${OUTPUT_DIR}
)

target_include_directories(VisualUI
    PUBLIC
        $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/src>
        $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}/VisualUI>
)

install(TARGETS VisualUI
    ARCHIVE DESTINATION $<$<CONFIG:Debug>:debug/>${CMAKE_INSTALL_LIBDIR}
    LIBRARY DESTINATION $<$<CONFIG:Debug>:debug/>${CMAKE_INSTALL_LIBDIR}
    RUNTIME DESTINATION $<$<CONFIG:Debug>:debug/>${CMAKE_INSTALL_BINDIR}
    INCLUDES DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/VisualUI
)

install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/src/"
    DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}/VisualUI"
    FILES_MATCHING PATTERN "*.h" PATTERN "*.hpp"
)
